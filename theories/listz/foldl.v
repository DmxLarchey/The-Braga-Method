(**************************************************************)
(*   Copyright                                                *)
(*             Jean-FranÃ§ois Monin           [+]              *)
(*             Dominique Larchey-Wendling    [*]              *)
(*                                                            *)
(*            [+] Affiliation VERIMAG - Univ. Grenoble-Alpes  *)
(*            [*] Affiliation LORIA -- CNRS                   *)
(**************************************************************)
(*      This file is distributed under the terms of the       *)
(*         CeCILL v2 FREE SOFTWARE LICENSE AGREEMENT          *)
(**************************************************************)

(* List traversal from right to left *)
Require Import List Utf8.
Import ListNotations.

Require Import lr.
(* The domain of ð”¾_foldl happens to be exactly ð”»lz as defined in lr_rec *)
Require Import lr_rec.


(* Here is a reference definition of List.fold_left, which follows the
same structural pattern as List.fold_right, but where lists are
traversed from right to left.

let rec foldl_ref f b l = fakematch l with
  | [] â†’ b
  | u +: z â†’ f (foldl_ref f b u) z

This can be actually programmed by reflecting this
decomposition of lists from the right using l2r.

let rec foldl_ref f b l = match l2r l with
  | Nilr â†’ b
  | Consr (u, z) â†’ f (foldl_ref f b u) z

This definition corresponds to the usual informal drawings explaining
the expected result. It is of course quite inefficient, however and it
can be seen as a specification of fold_left.

let rec foldl f b l = match l with
   | [] â†’ b
   | x :: l â†’ foldl f (f b x) l

This Coq scripts shows that foldl is actually equivalent to
foldl_ref.

The point is to get a Coq definition of foldl_ref, whereas
the recursion is not the usual structural recursion on lists.
Following the method exposed at Braga, we start with an
inductive graph corresponding to foldl_ref.
*)

(* -------------------------------------------------------------------------- *)
(* Relational Graph *)

Section sec_context.

Context {A B: Type}.
Implicit Type l u v : list A.
Implicit Type r : lr A.
Implicit Type x y z : A.
Implicit Type b : B.

Section sec_params_foldl.
Variable f: B â†’ A â†’ B.
Variable b0 : B.


Reserved Notation "l 'âŸ¼fl' b" (at level 70, format "l  âŸ¼fl  b").

Inductive ð”¾_foldl : list A â†’ B â†’ Prop :=
| FLnil : [] âŸ¼fl b0
| FLcons : âˆ€ {u z b},    u âŸ¼fl b   â†’  u +: z âŸ¼fl f b z
where "l âŸ¼fl b" := (ð”¾_foldl l b).

(* A technical variant of FLcons, for later convenience *)
Definition FLcons_lrl {u z b} : lrl u âŸ¼fl b  â†’  u +: z âŸ¼fl f b z.
  refine (fun G => FLcons _). pattern u. exact (down_llP _ u G).
Defined.

(* -------------------------------------------------------------------------- *)
(* Using the Braga method *)

(* foldl conform by construction (or "packed with conformness") *)

(* The explicit dependent pattern matching

   match l2r l as x return ** ð”»lz (r2l x) ... â†’ _ ** with

   ** ... ** added below, is not needed any more for Coq 8.11+ *) 

(* Shape of the goal before refine:
   ð”»lz (r2l (l2r l)) â†’
   (âˆ€y, r2l (l2r l) âŸ¼fl y â†’ l âŸ¼fl y) â†’
   {b | l âŸ¼fl b} *)
Let Fixpoint foldl_pwc l (D: ð”»lz l): {b | l âŸ¼fl b}.
Proof.
  gen_help l ð”¾_foldl. apply up_llP in D; revert D.
  refine (match l2r l as x return ð”»lz (r2l x) 
                                â†’ (âˆ€ y : B, r2l x âŸ¼fl y â†’ l âŸ¼fl y) 
                                â†’ _ with
          | Nilr      => Î» D T,  exist _ b0 _
          | Consr u z => Î» D T,
                 let (b, Gb) := foldl_pwc u (Ï€_ð”»lz D) in
                 exist _ (f b z) _
          end).
  - apply T; constructor 1.
  - apply T; constructor 2; exact Gb.
Defined.

(* Alternative definition, with just an equality in the Trojan horse
   -->
   + simpler goal before refine
   - structural inversion of D obtained after a rewriting step,
     cannot directly be put in the match
   - no explicit view of the rewriting steps
     (rewrite makes no difference between eq_ind and eq_recT)
*)
Let Fixpoint foldl_pwc_eq l (D: ð”»lz l): {b | l âŸ¼fl b}.
Proof.
  generalize (lrl_id l).
  (* r2l (l2r l) = l â†’ {b | l âŸ¼fl b} *)
  refine (match l2r l with
          | Nilr      => Î» E,  exist _ b0 _
          | Consr u z => Î» E,
                  let (b, Gb) := foldl_pwc_eq u _ in
                  exist _ (f b z) _
          end); simpl in E.
  - rewrite <- E in *. constructor 1.
  - rewrite <- E in D. apply (Ï€_ð”»lz D).
  - rewrite <- E in *. constructor 2. exact Gb.
Defined.

(* The reference function for foldl *)
Definition foldl_ref l (D : ð”»lz l) : B :=
  proj1_sig (foldl_pwc l D).

Lemma foldl_ref_corr_partial l (D: ð”»lz l) :
  l âŸ¼fl foldl_ref l D.
Proof.
  exact (proj2_sig (foldl_pwc l D)).
Qed.

(* -------------------------------------------------------------------------- *)
(* Version corresponding to OCaml fold_left *)
Fixpoint foldl b l : B :=
    match l with
    | [] => b
    | x :: l => foldl (f b x) l
    end.

(* foldl is compatible with +: in the following sense *)
Lemma foldl_consr b (u: list A) (z: A) :
  foldl b (u +: z) = f (foldl b u) z.
Proof.
  revert b. induction u as [|x u Hu]; intro b; simpl.
  - reflexivity.
  - rewrite Hu. reflexivity.
Qed.

(* Completeness of foldl wrt ð”¾_foldl follows *)
Theorem foldl_compl b l :  l âŸ¼fl b  â†’  b = foldl b0 l.
Proof.
  intro g. induction g as [ | u z b g Hg]; simpl.
  - reflexivity.
  - rewrite foldl_consr. rewrite Hg. reflexivity.
Qed.
(* Corollary for free: ð”¾_foldl is functional; but useless *)

(* Partial conformity wrt ð”¾_foldl: whenever l is in the domain,
   fold computes a good result *)
(* Induction on  ð”»lz l *)
Theorem foldl_corr_partial l : ð”»lz l  â†’  l âŸ¼fl foldl b0 l.
Proof.
  intro D. induction D as [ | u Gu z].
  - apply FLnil.
  - rewrite foldl_consr. apply (FLcons Gu).
Qed.

(* Total conformity wrt ð”¾_foldl follows independently *)
Corollary foldl_corr l :  l  âŸ¼fl  foldl b0 l.
Proof.
  apply foldl_corr_partial. apply ð”»lz_all.
Qed.

End sec_params_foldl.

(* -------------------------------------------------------------------------- *)
(* Partial conformity wrt foldl_ref: whenever foldl_ref terminates,
   fold computes the same result *)
Theorem foldl_equiv_partial :
     âˆ€ f b l (D: ð”»lz l), foldl f b l = foldl_ref f b l D.
Proof.
  intros. symmetry. apply foldl_compl. apply foldl_ref_corr_partial.
Qed.

(* Total conformity wrt foldl_ref follows independently *)
Corollary foldl_equiv_total :
    âˆ€ f b l, foldl f b l = foldl_ref f b l (ð”»lz_all l).
Proof.
  intros; apply foldl_equiv_partial.
Qed.

(* Additional remarks
- conformity of foldl wrt foldl_ref needs its completeness wrt ð”¾_foldl,
  not its conformity
- conformity of foldl wrt ð”¾_foldl is technically easier:
  an induction on the domain;
  in contrast, the definition foldl_ref requires a stronger recursion,
  including projective inversions (see lr_rec)
- as expected, termination is considered separately (with ð”»lz_all),
  whatever the approach (using a relational or a functional specification)
*)

End sec_context.

(* -------------------------------------------------------------------------- *)
(* Extraction *)

Require Import Extraction.
Extract Inductive list => "list" [ "[]" "(::)" ].
Recursive Extraction foldl_ref foldl.
(*

type 'a lr =
| Nilr
| Consr of 'a list * 'a

(** val l2r : 'a1 list -> 'a1 lr **)

let rec l2r = function
| [] -> Nilr
| x::lr0 ->
  (match l2r lr0 with
   | Nilr -> Consr ([], x)
   | Consr (lg, z) -> Consr ((x::lg), z))

(** val foldl_ref : ('a2 -> 'a1 -> 'a2) -> 'a2 -> 'a1 list -> 'a2 **)

let rec foldl_ref f b0 l =
  match l2r l with
  | Nilr -> b0
  | Consr (u, z) -> f (foldl_ref f b0 u) z

(** val foldl : ('a2 -> 'a1 -> 'a2) -> 'a2 -> 'a1 list -> 'a2 **)

let rec foldl f b = function
| [] -> b
| x::l0 -> foldl f (f b x) l0


*)


(* -------------------------------------------------------------------------- *)
